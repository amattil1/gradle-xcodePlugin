allprojects {
	apply plugin: 'java-gradle-plugin'
	apply plugin: 'groovy'

	def versionNumber = "0.21.2"

	if (project.hasProperty("versionNumber")) {
		versionNumber = project.versionNumber
	}
	if (project.hasProperty("versionSuffix")) {
		versionNumber += project.versionSuffix
	}
	if (project.hasProperty("buildNumber")) {
		versionNumber += "." + project.buildNumber
	}


	group = 'org.openbakery'
	version = versionNumber

	sourceCompatibility = "1.8"
	targetCompatibility = "1.8"

	repositories {
		mavenCentral()
		// Spock snapshots are available from the Sonatype OSS snapshot repository
		maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
	}


	dependencies {
		implementation localGroovy()
		implementation 'commons-io:commons-io:2.2'
		implementation 'commons-configuration:commons-configuration:1.10'
		implementation 'commons-collections:commons-collections:3.2.2'
		implementation 'commons-codec:commons-codec:1.10'

		testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
		testImplementation 'org.hamcrest:hamcrest-all:1.3'
		testImplementation 'org.objenesis:objenesis:1.2'
		testImplementation 'cglib:cglib-nodep:2.2.2'

		testImplementation platform("org.spockframework:spock-bom:2.0-groovy-3.0")
  	testImplementation "org.spockframework:spock-core"
		testImplementation gradleTestKit()
	}


	test {
	  useJUnitPlatform()
	}

}



project(':plugin') {
	dependencies {
		implementation project(':libxcode')
		implementation project(':libxcodetools')
		testImplementation project(':libtest')
	}
}


project(':libxcode') {
	dependencies {
		testImplementation project(':libtest')
	}
}

project(':libxcodetools') {
	dependencies {
		implementation project(':libxcode')
		testImplementation project(':libtest')
	}
}


project(':libtest') {
	dependencies {
		implementation project(':libxcode')
		implementation project(':libxcodetools')
	}
}


