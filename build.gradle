allprojects {
	apply plugin: 'java-gradle-plugin'
	apply plugin: 'groovy'

	def versionNumber = "0.21.2"

	if (project.hasProperty("versionNumber")) {
		versionNumber = project.versionNumber
	}
	if (project.hasProperty("versionSuffix")) {
		versionNumber += project.versionSuffix
	}
	if (project.hasProperty("buildNumber")) {
		versionNumber += "." + project.buildNumber
	}


	group = 'org.openbakery'
	version = versionNumber

	sourceCompatibility = "1.8"
	targetCompatibility = "1.8"

	repositories {
		mavenCentral()
		// Spock snapshots are available from the Sonatype OSS snapshot repository
		maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
	}


	dependencies {
		implementation localGroovy()
		implementation 'commons-io:commons-io:2.2'
		implementation 'commons-configuration:commons-configuration:1.10'
		implementation 'commons-collections:commons-collections:3.2.2'
		implementation 'commons-codec:commons-codec:1.10'
		implementation 'ch.qos.logback:logback-core:1.0.13'
		implementation 'org.codehaus.groovy:groovy-all:2.5.10'

		testImplementation 'junit:junit:4.13'
		testImplementation 'org.hamcrest:hamcrest-all:1.3'
		testImplementation group: 'org.spockframework', name: 'spock-core', version: '1.3-groovy-2.5'
		testImplementation 'org.objenesis:objenesis:1.2'
		testImplementation 'cglib:cglib-nodep:2.2.2'
		testImplementation('org.spockframework:spock-core:1.3-groovy-2.5') {
			exclude module: 'groovy-all'
		}
		testImplementation gradleTestKit()
	    testImplementation( 'com.athaydes:spock-reports:1.8.0' ) {
	        transitive = false
	    }
	}

	// Fix warnings when running tests in IDEA. Example:
	// SLF4J: Class path contains multiple SLF4J bindings.
	//    SLF4J: Found binding in [jar:file:/Users/ben/.gradle/caches/4.6/generated-gradle-jars/gradle-api-4.6.jar!/org/slf4j/impl/StaticLoggerBinder.class]
	// SLF4J: Found binding in [jar:file:/Users/ben/.gradle/caches/modules-2/files-2.1/ch.qos.logback/logback-classic/1.1.5/92353eb144695bba80b31f7bec4f36d871f230ac/logback-classic-1.1.5.jar!/org/slf4j/impl/StaticLoggerBinder.class]
	// SLF4J: See http://www.slf4j.org/codes.html#multiple_bindings for an explanation.
	// SLF4J: Actual binding is of type [org.gradle.internal.logging.slf4j.OutputEventListenerBackedLoggerContext]
	configurations.all {
		resolutionStrategy.dependencySubstitution {
			substitute module('ch.qos.logback:logback-classic') with module('org.slf4j:slf4j-api:1.7.+')
		}
	}
}



project(':plugin') {
	dependencies {
		implementation project(':libxcode')
		implementation project(':libxcodetools') 
		testImplementation project(':libtest')
	}
}


project(':libxcode') {
	dependencies {
		testImplementation project(':libtest')
	}
}

project(':libxcodetools') {
	dependencies {
		implementation project(':libxcode')
		testImplementation project(':libtest')
	}
}


project(':libtest') {
	dependencies {
		implementation project(':libxcode')
		implementation project(':libxcodetools')
	}
}
