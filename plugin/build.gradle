plugins {
	id 'maven-publish'
	id 'com.gradle.plugin-publish' version '0.16.0'
}


def publishURL = System.getenv("PUBLISH_URL")
def publishUser = System.getenv("PUBLISH_USER")
def publishPassword = System.getenv("PUBLISH_PASSWORD")

if (project.hasProperty("publishURL")) {
	publishURL = project.publishURL
}
if (project.hasProperty("publishUser")) {
	publishUser = project.publishUser
}
if (project.hasProperty("publishPassword")) {
	publishPassword = project.publishPassword
}


repositories {
	mavenCentral()
}


configurations {
	deployerJars
}



dependencies {
	implementation gradleApi()
	implementation 'com.squareup.okhttp3:okhttp:4.2.0'
	implementation 'org.openbakery.coverage:CoverageReport:0.9.4'
	deployerJars 'org.apache.maven.wagon:wagon-ssh:3.3.2'
}


project.archivesBaseName = 'xcode-plugin'

jar {
	from project.sourceSets.main.output
	from project(":libxcode").sourceSets.main.output
	from project(":libxcodetools").sourceSets.main.output
}

task sourcesJar(type: Jar) {
	from sourceSets.main.allSource
	from project(":libxcode").sourceSets.main.allSource
	from project(":libxcodetools").sourceSets.main.allSource
	classifier = 'sources'
}




publishing {
	publications {


		maven(MavenPublication) {
			groupId = 'org.openbakery.xcode-plugin'
			artifactId = 'xcode-plugin'
			version = project.version

			pom {
				pom.withXml {

					asNode().dependencies.'*'.findAll() {
						it.scope.text() == 'runtime' && project.configurations.implementation.allDependencies.find { dep ->
							return true
						}
					}.each { 
						if (it.artifactId.text().startsWith("libxcode")) {
							it.scope*.value = 'compile'
						}
					}

				}
				licenses {
					license {
						name = 'The Apache License, Version 2.0'
							url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
					}
				}
				developers {
					developer {
						id = 'renep'
							name = 'Ren√© Pirringer'
							email = 'rene@openbakery.org'
					}
				}
			}
			from components.java
		}
	}

	repositories {
		maven {
			//url = '~/tmp/repository'
			url "${publishURL}"
      credentials {
            username = "${publishUser}" 
            password = "${publishPassword}"
        }
    }
	}
}


pluginBundle {
	website = 'https://openbakery.org/gxp/'
	vcsUrl = 'https://github.com/openbakery/gradle-xcodePlugin'
	description = 'A gradle plugin for building Xcode projects!'
	tags = ['xcode', 'iOS', 'os x', "osx", "mac", "iphone", "ipad"]

	plugins {
		xcodePlugin {
			id = 'org.openbakery.xcode-plugin'
			displayName = 'Gradle Xcode plugin'
		}
	}
}
